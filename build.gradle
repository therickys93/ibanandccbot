apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
  }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'info.solidsoft.pitest'

javadoc {
    options {
        links( 'http://docs.oracle.com/javase/8/docs/api/' )
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        charSet 'UTF-8'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: "${buildDir}/classes/main", 
            exclude: ['it/itjustworks/ibanandccbot/server/**',
            'it/itjustworks/ibanandccbot/utilities/**'])
        })
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.restlet.org" }
}

dependencies {
    compile 'org.restlet.jse:org.restlet:2.3.9'
    compile 'org.restlet.jse:org.restlet.ext.jackson:2.3.9'
    compile 'com.github.pengrad:java-telegram-bot-api:2.3.1'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile 'org.mockito:mockito-core:2.7.14'
}

pitest {
    targetClasses = [ "it.itjustworks.ibanandccbot.*" ]
}

shadowJar {
    archiveName 'ibanandccbot.jar'
}

task stage() {
    dependsOn clean, shadowJar
}

tasks.stage.doLast() {
    delete 'build/classes'
    delete 'build/dependency-cache'
    delete 'build/tmp'
    delete fileTree(dir: 'build/libs', exclude: '*.jar')
}

shadowJar.mustRunAfter clean


